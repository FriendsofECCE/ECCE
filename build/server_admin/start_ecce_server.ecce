#!/bin/csh

set httpdserverroot = ##ECCESERVERROOT##/httpd

setenv ACTIVEMQ_HOME ##ECCESERVERROOT##/activemq

# check for allocated ports
set busyport = 0

set httpdport = `grep "^Listen" $httpdserverroot/conf/httpd.conf | awk '{print $2}'`
if (`netstat -a -n | grep LISTEN | grep "[:.]$httpdport " | wc -l` != 0) then
  echo "Apache HTTPD server port, $httpdport, already in use\!"
  set busyport = 1
endif

set jmsport = `grep openwire $ACTIVEMQ_HOME/conf/activemq.xml | head -n 1 | awk -F":" '{print $3}' | awk -F'"' '{print $1}'`
if (`netstat -a -n | grep LISTEN | grep "[:.]$jmsport " | wc -l` != 0) then
  echo "Java Messaging Service port, $jmsport, is already in use\!"
  set busyport = 1
endif

if ($busyport == 1) then
  goto port_exit
endif

if ( ! -e $httpdserverroot/bin/apachectl) then 
  echo "Unable to find $httpdserverroot/bin/apachectl"
  goto error_exit
endif

if ( ! -d $httpdserverroot/logs) then
  mkdir $httpdserverroot/logs
  chmod go-w $httpdserverroot/logs
endif

if ( -d /var/tmp/EcceServer$httpdport) then
  rm -rf /var/tmp/EcceServer$httpdport
endif

if ( ! -d /var/tmp/EcceServer$httpdport) then
  mkdir -p /var/tmp/EcceServer$httpdport
  chmod go-w /var/tmp/EcceServer$httpdport
  if (`whoami` == "root") then
    chown nobody /var/tmp/EcceServer$httpdport
  endif
else
  echo "Existing data server lock directory /var/tmp/EcceServer$httpdport could not be removed"
  goto error_exit
endif

if ( ! -d /var/tmp/EcceServer$httpdport) then 
  echo "Unable to create data server lock directory /var/tmp/EcceServer$httpdport"
  goto error_exit
endif

if ($?LD_LIBRARY_PATH) then
  setenv LD_LIBRARY_PATH ##ECCESERVERROOT##/httpd/lib:${LD_LIBRARY_PATH}
else
  setenv LD_LIBRARY_PATH ##ECCESERVERROOT##/httpd/lib
endif

$httpdserverroot/bin/apachectl start
if ($status != 0) then
  echo "Unable to execute apachectl start"
  goto error_exit
endif 

# java executable must be in $PATH
java -version >& /dev/null
if ($status != 0) then
  echo "Can't find java--unable to start messaging server"
  goto error_exit
endif

if ( ! -e $ACTIVEMQ_HOME/lib) then
  echo "Unable to find $ACTIVEMQ_HOME/lib"
  goto error_exit
endif

set CP=""
foreach file (${ACTIVEMQ_HOME}/lib/*.jar)
   set CP=${file}:${CP}
end
setenv CLASSPATH $CP

# activemq wrapper script called below uses $JAVA_HOME if it is set to invoke
# java, which can conflict with the version that ECCE thinks is being used so
# the best option is to force activemq to use the java we want it to
unsetenv JAVA_HOME

(${ACTIVEMQ_HOME}/bin/activemq | grep -v -e "ACTIVEMQ" -e "Loading" -e "AMQ" -e "Kaha" -e "help" -e "Transport") &
if ($status != 0) then
  echo "Unable to start messaging server"
  goto error_exit
endif

exit 0

error_exit:
  echo ""
  exit -1

port_exit:
  echo ""
  echo "Either an ECCE server is still running or some other service on this"
  echo "machine (such as another Apache server) has allocated these ports."
  echo "See the ECCE Installation and Administration Guide for how to change"
  echo "the ECCE port numbers to avoid conflicts."
  echo ""
  exit -1
